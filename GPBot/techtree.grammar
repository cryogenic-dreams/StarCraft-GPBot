<techtree> ::= <buildplan>
<buildplan> ::= (buildplan <buildings> <basicbuildings>)
<basicbuildings> ::= (supply_depot) | (refinery)
<buildings> ::= <building> | (buildings <building> <buildings>)
<building> ::= <barracks> | <engineering_bay> | <factory> | <bunker> | <academy> | <missile_turret> | <starport> | <armory> | <machine_shop> | <comsat_station> | <science_facility> | <control_tower> | <physics_lab> | <covert_ops> | <nuclear_silo>
<barracks> ::= (barracks <supply>)
<engineering_bay> ::= (engineering_bay <supply>)
<factory> ::= (factory <barracks> <supply>)
<bunker> ::= (bunker <barracks> <supply>)
<academy> ::= (academy <barracks> <supply>)
<missile_turret> ::= (missile_turret <engineering_bay> <supply>)
<starport> ::= (starport <factory> <supply>)
<armory> ::= (armory <factory> <supply>)
<machine_shop> ::= (machine_shop <factory> <supply>)
<comsat_station> ::= (comsat_station <academy> <supply>)
<science_facility> ::= (science_facility <starport> <supply>)
<control_tower> ::= (control_tower <starport> <supply>)
<physics_lab> ::= (physics_lab <science_facility> <supply>)
<covert_ops> ::= (covert_ops <science_facility> <supply>)
<nuclear_silo> ::= (nuclear_silo <covert_ops> <supply>)
<supply> ::= (supply)