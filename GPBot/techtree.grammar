<techtree> ::= <buildplan>
<buildplan> ::= (buildplan <units> <basicbuildings>)
<basicbuildings> ::= <basicbuilding> | (basicbuildings <basicbuilding> <basicbuildings>)
<basicbuilding> ::= (supply_depot) | (refinery) | <bunker> | <missile_turret> | <comsat_station>
<units> ::= <unit> | (units <unit> <units>)
<unit> ::= <marine> | <vulture> | <medic> | <tank> | <valkyrie> | <firebat> | <goliath> | <wraith> | <science_vessel> | <dropship> | <ghost> | <battlecruiser>
<barracks> ::= (barracks <supply>)
<engineering_bay> ::= (engineering_bay <supply>)
<factory> ::= (factory <barracks> <supply>)
<bunker> ::= (bunker <barracks> <supply>)
<academy> ::= (academy <barracks> <supply>)
<missile_turret> ::= (missile_turret <engineering_bay> <supply>)
<starport> ::= (starport <factory> <supply>)
<armory> ::= (armory <factory> <supply>)
<machine_shop> ::= (machine_shop <factory> <supply>)
<comsat_station> ::= (comsat_station <academy> <supply>)
<science_facility> ::= (science_facility <starport> <supply>)
<control_tower> ::= (control_tower <starport> <supply>)
<physics_lab> ::= (physics_lab <science_facility> <supply>)
<covert_ops> ::= (covert_ops <science_facility> <supply>)
<nuclear_silo> ::= (nuclear_silo <covert_ops> <supply>)
<marine> ::= (marine <barracks> <quantity>)
<vulture> ::= (vulture <factory> <quantity>)
<medic> ::= (medic <academy> <quantity>)
<tank> ::= (tank <machine_shop> <quantity>)
<valkyrie> ::= (valkyrie <armory> <control_tower> <quantity>)
<firebat> ::= (firebat <academy> <quantity>)
<goliath> ::= (goliath <armory> <quantity>)
<wraith> ::= (wraith <starport> <quantity>)
<science_vessel> ::= (science_vessel <control_tower> <science_facility> <quantity>)
<dropship> ::= (dropship <control_tower> <quantity>)
<ghost> ::= (ghost <academy> <covert_ops> <quantity>)
<battlecruiser> ::= (battlecruiser <control_tower> <physics_lab> <quantity>)
<supply> ::= (supply)
<quantity> ::= (quantity)